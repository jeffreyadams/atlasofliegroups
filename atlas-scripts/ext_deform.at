<basic.at
<extended.at
<polynomial.at

{ the name avoids being overridden by |KL_block| in KL_polynomial matrices }
set KL_block_raw (Param p, mat delta) = { emulate built-in |KL_block| }
    ([Param],int,mat,[vec],vec,vec,mat):
  let (regular_extblock,,,) = extended_block(p,delta)
  , (KL_mat,polys,stops) = raw_ext_KL(p,delta)
  then survivors =
    ## for q@j in regular_extblock do if q.survives then [j] else [] fi od
  , length = from_stops(stops)
  in ( regular_extblock,  first(for q in regular_extblock do q=p od)
     , KL_mat, polys, stops
     , vec: survivors
     , for q@j in regular_extblock { run through columns }
       do let lq=length(j), Q = finalize_extended(q,delta) in
         for si in survivors
         do minus_1^(lq-length(si))*Q[regular_extblock[si]].s_to_minus_1
         od
       od
     )
{ one can process the output by |cook_KL_block| from KL_polynomial_matrices.at }

set ed_verbose=true

set deformation_terms (Param p, mat delta) = ParamPol:
  assert(is_final(p) and is_fixed(delta,p),"improper parameter");
  let (B,P_ind_mat,polys) = partial_extended_KL_block(p,delta)
  then nB=#B, pev=for Q in polys do eval(Q,minus_1) od
  then P_mat= for col in P_ind_mat do for entry in col do pev[entry] od od
  in
  if =nB or B~[0]!=p
  then null_module(p) { if |p| itself is not among survivors, return 0 }
  else { compute final column |acc| of |Q| matrix by solving linear system
         $P'*acc=e_p$ with $P'$ a matrix of Split, columns from |KL_sum_at_s| }
    if ed_verbose then prints("Deformation terms for ",p) fi;
    let remainder = null(nB-1)#1 { start with standard basis vector for |p| }
    , solution=null(nB)
    , !opposite_parity=1-length(p)%2 { opposite to that of |length(p)| }
    , !on_p = orientation_nr(p)
    in
    for q_ind : nB
    ~do let contrib=remainder[q_ind]*P_mat[q_ind] in
      if ed_verbose
      then prints("Contribution: ",remainder[q_ind],"*",P_mat[q_ind])
      fi;
      remainder -:= contrib
    ; if length(B[q_ind])%2=opposite_parity
      then solution +:= contrib
      ; if ed_verbose then prints("added, giving ",solution) fi
      fi
    od
  ; null_module(p)+
    for c@i in solution
    do { twist (1-s)*solution according to orientation numbers of its terms }
      let x=B[i] then (q,r)=(on_p-orientation_nr(x))\%2 in
      assert(=r,"odd orientation");
      (minus_1^q*c*one_minus_s,x) { we have |s^q*c*(1-s) = (-1)^q*c(1-s)| }
      { for the same reason we replaced |s| by |-1| in computations giving |c| }
    od
  fi

set recursive_deform (Param p, mat delta, (Param->ParamPol)terms) = KTypePol:
 assert(is_final(p) and is_fixed(p,delta),"improper parameter");
 let G=p.real_form in
 (1,p). { apply following recursive function to this initial argument }
 ( rec_fun full_def (Split sc,Param p) KTypePol:
     { |sc| will simply scale the resulting ParamPol, |delta| is implicit }
    begin
      if ed_verbose then prints("in recursive function full_def with: sc=", sc, ", p=", p) fi;
      assert(is_standard(p),"non standard parameter encountered in deformation");
{      let acc = G.null_module, at_nu0 = K_type_pol_extended(p,delta)}
      let acc = G.null_module, at_nu0 = sc*K_type_pol_extended(p,delta) {MISSING sc?}
      in
      if ed_verbose then prints("deformation at nu=0 contributes ",at_nu0) fi;
      for factor in reducibility_points(p)
      ~ { direction of deformation is down, towards 0; this is not essential }
      do
	let (pf,flipped)=scale_extended(p,delta,factor) { next deformation }
	then def= s^#flipped * terms(pf)
	in
	let ()=prints("sc=",sc) in
        if ed_verbose then prints("adding deformation term for p=",pf, new_line,
	                          "adding:", def)
        fi;
        acc+:=def
      od{for factor}
     ;prints("acc is NOW: ", acc);
     for k@q in acc { traverse the sum of the deformation terms spun off }
      do { apply |full_def| recursively to all those terms }
        at_nu0 +:= full_def(sc*k,q) { gather harvest from those deformations }
      od
     ;at_nu0
    end
  )

set recursive_deform (Param p, mat delta) = KTypePol:
   recursive_deform(p,delta,(Param q)ParamPol:deformation_terms(q,delta))

set twisted_recursive_deform (Param p) = KTypePol:
   recursive_deform(p,p.real_form.distinguished_involution,twisted_deform@Param)

set twisted_full_deform = recursive_deform@(Param,mat)
