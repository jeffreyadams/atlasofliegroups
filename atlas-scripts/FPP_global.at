<K_highest_weights.at {for all_lambda_differential_zero}
<lattice.at {for vec_solve}
{ <chopping_facets_fast.at {for chop_coroots(G, vertices)}}
<facet_classes.at {for ordering on facets}
<hash.at {for KPolHash}
<TWOHEIGHTS {for HTA and HT}
<cohom_reduction {for possible use of cohomological induction}
<FPP_local.at

{functions for computing the entire FPP_unitary dual}
{typically functions take a Param_hash Uhash and fill it with all
unitary reps having infinitesimal character in the FPP. Each p is
replaced by herm_center(p), at the barycenter of its local facet,
so that the lists are finite.}

{. This controls the maximum denominator size kept in chop_coroots,
used in moving unitarity information from a Levi to G. Large values
allow more chopped facets, and so convey more information to G; but
small values are faster. Too small a value cannot lead to wrong
answers, but only to running more is_unitary tests .}
set chop_bound = int: 6

{next functions aim to compute FULL spherical dual in non-split case,
when several different x give spherical reps}

set spherical_xs(RealForm G) = [KGBElt]:
    let real_rank = rank(x_open(G).involution + 1)
    in for x in KGB(G)
       do if rank(x.involution + 1) = real_rank
       	  then [x]
	  else []
	  fi
       od.##

{this seems to fail to find all relevant lambdas for some unequal rank cases.
Following function FPP_lambdas_big seems OK?}
set FPP_lambdas(KGBElt x) = [ratvec]:
    let G=x.real_form, twists = all_lambda_differential_0(x)
    then zeroPol = ParamPol: 0*G.trivial
    then rho = G.rho, verts = FPP_vertices(G)
    then th1 = x.involution + 1
    then lamthlams = [vec]:
    	 for v in verts
    	 do if ((th1*v).denom = 1) then [ratvec_as_vec(th1*v)]
	    else []
	    fi
	 od.##.no_reps
    then cands = [ratvec]:
    	 for lamthlam in lamthlams
    	 do case vec_solve(th1,lamthlam - th1*rho)
	    | else [ratvec]: []
	    | solution(lr):
	      let lambda = rho + lr
	      in for lambda0 in twists do lambda + lambda0 od
	    esac
	  od.##
    in for lambda in cands
       do let p = parameter(x,lambda,0*lambda)
       	  then lambdaNorm = p.lambda
	  in if 1*p = zeroPol
       	     then []
	     else [lambdaNorm]
	  fi
       od.##

set all_FPP_vertices(RootDatum G) = [ratvec]:
    let factors = for S in simple_factors(G)
    		  do FPP_vertices(S,-1)
		  od
    in case #factors
       in [ratvec:null(rank(G))/1],
       factors[0],
       for v in factors[0]
       do for w in factors[1]
       	  do v+w
	  od
       od.##
       esac

{each term is a candidate lambda0 (mod comp gp char twists) and the
corresponding Lvd, Perm, mapAct}
set FPP_lambdas_big(KGBElt x, VertexData vd, mat_hash inv_hash, [vec_hash]
    dlambda_hashes, [[(VertexData, vec, vec)]] LvdEtcs) =
    		    [(ratvec,VertexData, vec, vec)]:
    let G=x.real_form
    then zeroPol = ParamPol: 0*G.trivial, theta = x.involution
    then i = inv_hash.lookup(theta)
    then rho = G.rho, verts = all_FPP_vertices(G)
    then th1 = theta + 1
    then lamthlams = [vec]:
    	 for v in verts
    	 do if ((th1*v).denom = 1) then [ratvec_as_vec(th1*v)]
	    else []
	    fi
	 od.##.no_reps
    in [(ratvec, VertexData, vec, vec)]:
    	 for lamthlam in lamthlams
    	 do case vec_solve(th1,lamthlam - th1*rho)
	    | else [(ratvec,VertexData,vec,vec)]: []
	    | solution(lr):
	      let lambda0 = rho + lr
	      then dlambda0 = ratvec_as_vec(lambda0 + theta*lambda0)
    	      then j = dlambda_hashes[i].match(dlambda0)
              then () = if j = #LvdEtcs[i]
                      	then
                      	LvdEtcs[i]:=LvdEtcs[i]#localFD_Lvd(x,lambda0,vd)
                      	fi
              then (Lvd, Perm, mapAct) = LvdEtcs[i][j]
	      in if #Lvd.list > 0 and
	      	    1*parameter(x,lambda0,0*lambda0) != zeroPol
		 then [(lambda0, Lvd, Perm, mapAct)]
	      	 else []
		 fi
	      esac
	  od.##

set unitary_hash = make_Param_hash()

{load a bunch of Param_hash's into one}
set transfer([Param_hash] Us, Param_hash target) = void:
    for U in Us
    do for j:U.size()
       do target.match(U.index(j))
       od
    od

{derived simple factors of all Levis of theta-stable P, omitting P=G}
set DSFLevis(RealForm G) = [RealForm]:
    for P in theta_stable_parabolics(G)~[1:]
    do derived_simple_factors(P.Levi) od.##

set DSFLevi_hash(RealForm G) = RealForm_hash:
    make_RealForm_hash(DSFLevis(G), hash_code@(RealForm,int))

set FPP_unitary_hash_general(RealForm G,Param_hash Uhash,
    (KGBElt,ratvec,VertexData,VertexData,vec,vec,KTypePol_hash,Param_hash->[Param])
    local) = void:
  if is_compact(G)
  then for p in all_parameters_gamma(G,G.rho)
       do Uhash.match(p) od
  else
    let inv_hash = make_mat_hash(), vd = FPP_vertex_data(G)
    then () = for x in KGB(G) do inv_hash.match(x.involution) od
    then NumInvs = inv_hash.size(), pol_hash = make_KTypePol_hash()
    then dlambda_hashes = for i:NumInvs do make_vec_hash() od
    then LvdEtcs = for i:NumInvs do [(VertexData,vec,vec)]:[] od
    then old_facet_verbose = facet_verbose
    then () = test_verbose:=false
    then () = facet_verbose:=false
    then () = deform_flag :=true
    then () = unipotents_to_hash(G,Uhash)
    then () = for x in KGB(G)
    	    ~ do {KGB} if(old_facet_verbose)
      	      	 then prints("KGBElt #", let (,m) = %x in m, ", #Uhash = ",
		     Uhash.size(), ", #tests = ",
		     unitary_test_counter.use_count(), ", #def lookups = ",
		     deformLookupCounter.use_count(), ", #def calcs = ",
		     deformCalcCounter.use_count())
	 	 fi;
	 let twists = all_lambda_differential_0(x)
      	 then FPPbig = FPP_lambdas_big(x, vd, inv_hash, dlambda_hashes,
	      LvdEtcs)
	 then () = for (lambda0, Lvd, Perm, mapAct)@k in FPPbig
      	      	   do if old_facet_verbose and k%4 = 0
	      	      then prints("  working on ",k,"th dlambda out of ",
		      	   	     #FPPbig)
	    	      fi;
		      for tau in twists
	      	      do local(x, lambda0+tau, vd, Lvd, Perm, mapAct,
		      	 	  pol_hash, Uhash)
		      od
		   od
	   in ()
       	 od {KGB}
    then () = facet_verbose:=old_facet_verbose
    in prints("Number of FPP-unitary facets = ",Uhash.size())
    fi

set FPP_unitary_hash_one_level(RealForm G,Param_hash Uhash) = void:
    FPP_unitary_hash_general(G, Uhash, local_test_GEO_hash_one_level@(
    KGBElt,ratvec,VertexData,VertexData,vec,vec, KTypePol_hash, Param_hash))

set FPP_unitary_hash_no_ht(RealForm G,Param_hash Uhash) = void:
    FPP_unitary_hash_general(G, Uhash, local_test_GEO_hash_no_ht@(
    KGBElt,ratvec,VertexData,VertexData,vec,vec,KTypePol_hash,
    Param_hash))

set FPP_unitary_hash2(RealForm G,Param_hash Uhash) = void:
    FPP_unitary_hash_general(G, Uhash, local_test_GEO_hash2@(
    KGBElt,ratvec,VertexData,VertexData,vec,vec,KTypePol_hash,
    Param_hash))

set FPP_unitary_hash(RealForm G,Param_hash Uhash) = void:
    FPP_unitary_hash_general(G, Uhash, local_test_GEO_hash@(
    KGBElt,ratvec,VertexData,VertexData,vec,vec,KTypePol_hash,
    Param_hash))

{ lists real and/or complex simple factors }
set simple_factors(RealForm G) = [RealForm]:
    let delta=G.distinguished_involution, tf=KGB(G,0).torus_factor,
	simpleFactors = simple_factors(RootDatum: G),
	Factors = [RealForm]:[]
    in
    for rd@i in simpleFactors
    do
    let root = simple_roots(rd)[0]
    then newRoot = delta*root
    then simpleRootIndex = simple_root_index(G,root),
	 newSimpleRootIndex = simple_root_index(G,newRoot)
    then diagram = diagram_component(G,simpleRootIndex),
	 newDiagram = diagram_component(G,newSimpleRootIndex)
    in  if diagram=newDiagram then Factors##:=
	 [real_form(inner_class(rd,delta), delta, tf)]
	 else if diagram[0] < newDiagram[0] then Factors##:=
	 [real_form(inner_class(sub_datum(G,diagram##newDiagram),
		delta), delta, tf)]
	      fi
	fi
    od; Factors

set derived_simple_factors(RealForm G) = [RealForm]:
    for S in simple_factors(G) do derived(S) od

{(M,N): M is natural restriction from X^*(G) onto X^*(D).
 N is right inverse of M.}
set derived_infos(RealForm G) = (mat,mat):
    let (,M) = derived_info(G) in (M,required_solution(M, id_mat(G.semisimple_rank)))

{descend a parameter for G to all simple factors of theta-stable Levi support(x)}

set descents(Param p) = [Param]:
    let  x = p.x, G = p.real_form, lambda = p.lambda, nu = p.nu
    then P = Parabolic: (support(x), x) {, theta = x.involution}
    then L = P.Levi
    then lambdaL = lambda - G.rho + L.rho, nuL = nu, xL = inverse_embed_KGB(x,L)
    then SF = simple_factors(L)
    then xSs = for S in SF do inverse_embed_KGB(xL,S) od
    then nuSs = for xS in xSs do (nu - xS.involution*nu)/2 od
    in for S@j in SF do parameter(xSs[j], lambda - G.rho + S.rho, nuSs[j]) od
    {
    then DSF = for S in SF do derived(S) od
    then DSFinfos = for S in SF do derived_infos(S) od
    }

