<basic.at
<combinatorics.at { for |partition_pairs|, |pairs_of_total_sum| }
<character_tables.at
<classical_W_classes_and_reps.at { for type abbreviation |BC_class|, |cycles| }



{     type C     }

set BC_classes(int n) = [BC_class]:
   for PQ in partition_pairs(n) do to_cycles(PQ) od

{ word representative in types B and C of conjugacy class for partition pair;
  the parts of P correspond to are cycles without flip, those of Q with flip }
set class_word_BC(Partition P,Partition Q) = [int]:
(  assert((P:=P.strip_to_partition).>, "P is not a partition")
;  assert((Q:=Q.strip_to_partition).>, "Q is not a partition")
;  let nP=sum(P), nQ=sum(Q)
   ,  Pbars = let sum=minus_1 in for p in P do sum+:=p od
   ,  Qbars = let sum=minus_1 in for q in Q do sum+:=q od
then  P_gens = complement(nP,Pbars) { for P part, do the same as in type A }
   ,  Q_gens = { each cycle from Q gets a sign flip when its end is sensed }
      for i:nQ
      do let pos=nP+i { position in [0,n) where we are }
      in if isnt_member(Qbars)(i) then [pos] { extend our cycle }
         else { tack on a sign flip at the final element of the cycle }
	    for j:nQ-i from pos do j od## for j:nQ-i-1 from pos do j ~od
	 fi
      od.##
in P_gens##Q_gens
)

set conjugacy_class_Sp(BC_class cycles) = WeylElt:
   let n=cycles.rank in W_elt(Sp(n+n),class_word_BC(cycles.to_partition_pair))

{ for general type C root data, we need to look up and use the diagram order }
set conjugacy_class_C(RootDatum rd) = (BC_class->WeylElt):
   let map = test_simple_type("C",rd) then n=#map in
   (BC_class cycles) WeylElt:
(  let size = rank(cycles)
in assert(size=n,"Cycles sum to "+size+" rather than "+n)
;  W_elt(rd,for s in class_word_BC(cycles.to_partition_pair) do map[s] od)
)

set cycle_code_Sp(WeylElt w) = vec: { assuming |w| is for |Sp(2*n)| }
  let n=w.root_datum.rank in signed_cycle_type_code((vec:for i:n do i+1 od)*w)

set class_table_C(int n) = WeylClassTable:
   let rd=Sp(n+n), classes = [BC_class]: BC_classes(n)
in W_class_table( rd, for cycles in classes do conjugacy_class_Sp(cycles) od
		, (WeylElt w)int:
		    cycle_class_order(cycle_code_Sp(w).as_signed_cycles)
		, (WeylElt w0) (WeylElt->bool): let code=cycle_code_Sp(w0) in
		    (WeylElt w)bool: cycle_code_Sp(w)=code
		)

set class_table_C(RootDatum rd) = WeylClassTable:
  let classes = [BC_class]: BC_classes(rd.semisimple_rank)
  , type_code(WeylElt w) = [int]: signed_cycle_type_code(classic_permutation(w))
  , class_rep = (BC_class->WeylElt): conjugacy_class_C(rd)
  in W_class_table( rd, for cycles in classes do class_rep(cycles) od
		  , (WeylElt w)int:
		      cycle_class_order(type_code(w).as_signed_cycles)
		  , (WeylElt w0) (WeylElt->bool): let code=type_code(w0) in
		      (WeylElt w)bool: type_code(w)=code
		  )

set BC_irreps(int n) = [BC_irrep]:
   pairs_of_total_sum(n,(int k):partitions(k)~[:]) { list [k],..,ones(k) }

set character_table_C(int n) = CharacterTable:
   let class_list =  BC_classes(n)
   then classes = [WeylElt,classical_class,string]:
      for cycles in class_list
      do (conjugacy_class_Sp(cycles), cycles.class_BC,cycles.cycles_string)
      od
   ,  irreps = [classical_irrep,string,[int]]:
      for rep in BC_irreps(n)
      do ( rep.irrep_BC, rep.irrep_string
         , for cycles in class_list do hyperoctahedral_character(rep,cycles) od
	 )
      od
   in character_table(class_table_C(n),classes,irreps)

set character_table_C(RootDatum rd) = CharacterTable:
   let n=rd.semisimple_rank
   , class_rep = (BC_class->WeylElt): conjugacy_class_C(rd)
   then class_list =  BC_classes(n)
   then classes = [WeylElt,classical_class,string]:
      for cycles in class_list
      do (class_rep(cycles), cycles.class_BC,cycles.cycles_string)
      od
   ,  irreps = [classical_irrep,string,[int]]:
      for rep in BC_irreps(n)
      do ( rep.irrep_BC, rep.irrep_string
         , for cycles in class_list do hyperoctahedral_character(rep,cycles) od
	 )
      od
   in character_table(class_table_C(rd),classes,irreps)
