< induction.at {for theta_stable_quasi_data}
< unity.at {for deform_support}
< hermitian.at {for to_ht}
<K_types.at {for branch_std}
<time.at {for print_time_string}
<cohom_reductionND {for factor_params, factor_K_types}
{trying to get at bottom layer K-types. Use D for bound on height, d
for bound on number of terms. The "min" refers to the theta-stable
parabolic built by theta_stable_quasi_data. Parabolics _containing_
that one should certainly be supported; there's a chance also of
getting useful information from parabolics _not_ containing it.}
{"pairs" is...}

{K_signature_irrB is hermitian_form_irr_to_ht; K_signature_irr is hermitian_form_irr truncated.
Haven't figured out which is faster when.}
set min_bottom_verbose = false

set deform_support(Param p, int D) = KTypePol:
    to_ht(deform_support(p), D)

set two_rho_u_check(KGPElt Q) = vec:
    two_rho_check(Q.real_form) - two_rho_check(Q.Levi)

{Each (muG,muL) in output list is a Q-bottom layer K-type for p with (Q,pL) =
theta_stable_quasi_data(p). D is the largest height to which to compute
K-types. For now assume K connected.}
set min_bottom(Param p, int D) = [(KType,KType)]:
    let (Q,pL) = theta_stable_quasi_data(p), {x = p.x, lambda = p.lambda}
    	G = p.real_form
    then L = Q.Levi
    then xcL = KGB(L,0)
    then xcG = embed_KGB(xcL, G), LKTG = LKTs(p)[0], LKTL = LKTs(pL)[0]
    then TRUCH = two_rho_u_check(Q)
    then bottom = TRUCH*(highest_weight(LKTG, xcG).mu)
    then too_many_mus = monomials(branch_std(p,D))
    { monomials(deform_support(p,D)) }
    then mus = for mu in too_many_mus
       	       do if {height(mu) > height(LKTG) and}
	       	      TRUCH*(highest_weight(mu, xcG).mu) = bottom
	       	  then [mu]
	  	  else []
	  	  fi
	  	od.##
    then shift = highest_weight(LKTG,xcG).mu - highest_weight(LKTL,xcL).mu
    in  for mu in mus
    	do (mu, K_types(K_highest_weight_from_K0_weight(xcL,
       	       	       highest_weight(mu,xcG).mu - shift))[0])
	od

{each (tau,tauL) in output list is a bottom layer
K-type and the corr L\cap K-type. ASSUME K CONNECTED
Lists include LKT pairs, so always nonempty.}

set min_L_bottom(Param p, int D) = [(KType,KType)]:
    let (Q,pL) = theta_stable_quasi_data(p), {x = p.x, lambda = p.lambda,}
    	G = p.real_form
    then L = Q.Levi
    then xcL = KGB(L,0)
    then xcG = embed_KGB(xcL, G), LKTG = LKTs(p)[0],
    	 LKTL = LKTs(pL)[0]
    then shift = K0_param(LKTG, xcG).lambda_minus_rho - K0_param(LKTL,xcL).lambda_minus_rho
    	 {highest_weight(LKTG,xcG).mu - highest_weight(LKTL,xcL).mu}
    then DL = D-height(p) + height(pL)
    then too_many_tauLs = monomials(branch_std(pL,DL)) {reps of L\cap K}
    then K0 = K_0(xcG), LK0 = K_0(xcL)
    then tauLs =  for tauL in too_many_tauLs
       	      	  do let weight = K0_param(tauL,xcL).lambda_minus_rho + shift
		  in if {height(tauL) > 0 and}
		       	   is_dominant(K0,weight)
       	  	    then [(weight,tauL)]
	  	    else []
	  	    fi
		 od.##
    in for (weight,tauL) in tauLs
       do (K_types(K_highest_weight_from_K0_weight(xcG, weight))[0] ,tauL) od

{meant to be applied to quasisplit simple L}
set next_heights(RealForm L) = int:
    if not is_quasisplit(L) then 0
    elif is_abelian(L) then 0
    else max(for p in all_parameters_gamma(L, null(L.rank))
    	     do next_heights(p, (semisimple_rank(L) + 1)\2)
	     od.##)
    fi
 
set_type
[QRF_sig_tables =
( ( -> int) real_form_size
  , ( -> ) clear
  , ( -> vec) K_type_size {number of fine K-types for each real form}
  , ( -> vec) K_type_pol_size
  , ( -> vec) Param_size
  , (KType -> KTypePol) branch_entry {branch std to height}
  , (KType ->) branch_append {branch std to height, don't report}
  , (Param -> (KTypePol,KTypePol)) entry
  , (Param ->) append
  , (int -> RealForm) real_form_index
  , (int -> int) real_form_height
  , ((int,int) -> KTypePol) branch_index
  , ((int,int) -> (KTypePol,KTypePol)) sig_pol_index
  , ((int,int) -> vec) sig_vec_index
  , ( -> int) bottom_branch_time
  , ( -> int) bottom_sig_time
  )
]

set make_QRF_sig_tables () = QRF_sig_tables:
(  let real_form_hash = make_RealForm_hash()
   then param_hashes = [Param_hash] : [] {one for each real form}
   then K_type_hashes = [KType_hash] : [] {one for each real form; list of fine K-types}
   then K_type_pol_hashes = [KTypePol_hash] : []  {one for each real form}
   then branches = [vec] : [] {one vec for each real form; ith entry is index of branching KTypePol for KType #i}
   then sigs = [[vec]]: [] {one [vec] for each real form, one vec for each Param}
   then heights = vec: [] {jth entry is height to compute for jth real form}
   then bottom_branch_time = int: 0
   then bottom_sig_time = int: 0
   then clear() = void:
       let () = real_form_hash.clear()
       then () = param_hashes:=[]
       then () = K_type_hashes:=[]
       then () = K_type_pol_hashes := []
       then () = branches := []
       then () = sigs := []
       then  () = bottom_sig_time := 0
       then  () = bottom_branch_time := 0
       in ()
   in 
   ( ( -> int): { real_form_size } @int: real_form_hash.size()
   , ( -> ): {clear} clear
   , ( -> vec): {K_type_size} @vec: for hash in K_type_hashes do hash.size() od
   , ( -> vec): {K_type_pol_size} @vec: for hash in K_type_pol_hashes do hash.size() od
   , ( -> vec): {Param_size} @vec: for hash in param_hashes do hash.size() od
   , (KType -> KTypePol): {entry} (KType mu) KTypePol:
     begin
     	let RF = real_form_hash.size()
	then rf = real_form_hash.match(mu.real_form)
	then () = if rf = RF
	     	  then let () = param_hashes := (param_hashes next param_hashes:= [])
		       	       	 	     #make_Param_hash()
		       then () = K_type_hashes := (K_type_hashes next K_type_hashes:= [])
		       	       	 	     #make_KType_hash()
		       then () = K_type_pol_hashes := (K_type_pol_hashes next K_type_pol_hashes:= [])
		       	       	 	     #make_KTypePol_hash()
		       then () = heights := heights#next_heights(mu.real_form)
		       then () = sigs := (sigs next sigs := [])
		       	    	     # []
		       then () = branches #:= null(0){(branches next branches := [])
		       	    	     # []}
		       in ()
		  fi
	then KTYPENUM = K_type_hashes[rf].size()
	then ktypenum = K_type_hashes[rf].match(mu)
	then () = if ktypenum = KTYPENUM {haven't computed with mu before}
	     	  then let start_branch = elapsed_ms()
	     	       then pol = branch_std(mu,heights[rf])
		       then () = bottom_branch_time +:= elapsed_ms() - start_branch
	     	       then m = K_type_pol_hashes[rf].match(pol)
		       in branches[rf] #:= m
		  fi
	then () = assert(real_form_hash.size() = #K_type_pol_hashes,"QRF_sig_table damaged")
        then () = assert(real_form_hash.size() = #K_type_hashes,"QRF_sig_table damaged")
	then () = assert(real_form_hash.size() = #param_hashes,"QRF_sig_table damaged")
	then () = assert(#sigs[rf] = param_hashes[rf].size(), "QRF_sig_table damaged")
	then () = assert(#branches[rf] = K_type_hashes[rf].size(), "QRF_sig_table damaged")
	in K_type_pol_hashes[rf].index(branches[rf][ktypenum])
     end
   , (KType ->): {append_k_type} (KType mu):
     begin
     	let RF = real_form_hash.size()
	then rf = real_form_hash.match(mu.real_form)
	then () = if rf = RF
	     	  then let () = param_hashes := (param_hashes next param_hashes:= [])
		       	       	 	     #make_Param_hash()
		       then () = K_type_hashes := (K_type_hashes next K_type_hashes:= [])
		       	       	 	     #make_KType_hash()
		       then () = K_type_pol_hashes := (K_type_pol_hashes next K_type_pol_hashes:= [])
		       	       	 	     #make_KTypePol_hash()
		       then () = heights := heights#next_heights(mu.real_form)
		       then () = sigs := (sigs next sigs := [])
		       	    	     # []
		       then () = branches #:= null(0)
		       in ()
		  fi
	then KTYPENUM = K_type_hashes[rf].size()
	then ktypenum = K_type_hashes[rf].match(mu)
	then () = if ktypenum = KTYPENUM {haven't computed with mu before}
	     	  then let start_branch = elapsed_ms()
	     	       then pol = branch_std(mu,heights[rf])
		       then () = bottom_branch_time +:= elapsed_ms() - start_branch
	     	       then m = K_type_pol_hashes[rf].match(pol)
		       in branches[rf] #:= m
		  fi
	then () = assert(real_form_hash.size() = #K_type_pol_hashes,"QRF_sig_table damaged")
        then () = assert(real_form_hash.size() = #K_type_hashes,"QRF_sig_table damaged")
	then () = assert(real_form_hash.size() = #param_hashes,"QRF_sig_table damaged")
	then () = assert(#sigs[rf] = param_hashes[rf].size(), "QRF_sig_table damaged")
	then () = assert(#branches[rf] = K_type_hashes[rf].size(), "QRF_sig_table damaged")
	in ()
     end
   , (Param -> (KTypePol,KTypePol)): {entry} (Param p)
     (KTypePol,KTypePol):
     begin
	let RF = real_form_hash.size()
	then rf = real_form_hash.match(p.real_form)
	then () = if rf = RF
	     	  then let () = param_hashes := (param_hashes next param_hashes:= [])
		       	       	 	     #make_Param_hash()
		       then () = K_type_pol_hashes := (K_type_pol_hashes next K_type_pol_hashes:= [])
		       	       	 	     #make_KTypePol_hash()
		       then () = heights := heights#next_heights(p.real_form)
		       then () = sigs := (sigs next sigs := [])
		       	    	     # []
		       in ()
		  fi
	then PNUM = param_hashes[rf].size()
	then pnum = param_hashes[rf].match(p)
	then () = if pnum = PNUM {haven't computed with p before}
	     	  then let start_sig = elapsed_ms()
	     	       then (pos,neg) = K_signature_irrB(p,heights[rf])
		       then () = bottom_sig_time +:= elapsed_ms() - start_sig
	     	       then (p,q) = (K_type_pol_hashes[rf].match(pos), K_type_pol_hashes[rf].match(neg))
		       in sigs[rf] := if #sigs[rf] = 0 then [[p,q]]
		     	      	 else (sigs[rf] next sigs[rf] := [])
		       		    # [p,q]
				      fi
		  fi
	then () = assert(real_form_hash.size() = #K_type_pol_hashes,"QRF_sig_table damaged")
	then () = assert(real_form_hash.size() = #param_hashes,"QRF_sig_table damaged")
	then () = assert(#sigs[rf] = param_hashes[rf].size(), "QRF_sig_table damaged")
	in (K_type_pol_hashes[rf].index(sigs[rf][pnum][0]), K_type_pol_hashes[rf].index(sigs[rf][pnum][1]))
     end
   , (Param -> ): {append} (Param p):
     begin
 	let RF = real_form_hash.size()
	then rf = real_form_hash.match(p.real_form)
	then () = if rf = RF
	     	  then let () = param_hashes := (param_hashes next param_hashes:= [])
		       	       	 	     #make_Param_hash()
		       then () = K_type_pol_hashes := (K_type_pol_hashes next K_type_pol_hashes:= [])
		       	       	 	     #make_KTypePol_hash()
		       then () = heights := heights#next_heights(p.real_form)
		       then () = sigs := (sigs next sigs := [])
		       	    	     #[]
		       in ()
		  fi
	then PNUM = param_hashes[rf].size()
	then pnum = param_hashes[rf].match(p)
	then () = if pnum = PNUM {haven't computed with p before}
	     	  then let start_sig = elapsed_ms()
	     	       then (pos,neg) = K_signature_irrB(p,heights[rf])
		       then () = bottom_sig_time +:= elapsed_ms() - start_sig
		       then (p,q) = (K_type_pol_hashes[rf].match(pos), K_type_pol_hashes[rf].match(neg))
		       in sigs[rf] := if #sigs[rf] = 0 then [[p,q]]
		     	      	 else (sigs[rf] next sigs[rf] := [])
		       		    # [p,q]
				      fi
		  fi
	then () = assert(real_form_hash.size() = #K_type_pol_hashes,"QRF_sig_table damaged")
	then () = assert(real_form_hash.size() = #param_hashes,"QRF_sig_table damaged")
	then () = assert(#sigs[rf] = param_hashes[rf].size(), "QRF_sig_table damaged")
	in ()
     end
   , (int -> RealForm): {real_form_index} (int j): real_form_hash.index(j)
   , (int -> int): {real_form_height} (int j): heights[j]
   , ((int,int) -> KTypePol): {branch_index} (int rf, int ktypenum) KTypePol:
     K_type_pol_hashes[rf].index(branches[rf][ktypenum])
   , ((int,int) -> (KTypePol,KTypePol)): {sig_pol_index} (int rf, int pnum) (KTypePol,KTypePol):
     (K_type_pol_hashes[rf].index(sigs[rf][pnum][0]), K_type_pol_hashes[rf].index(sigs[rf][pnum][1]))
   , ((int,int) -> vec): {sig_vec_index} (int rf, int pnum) vec:
     sigs[rf][pnum]
   , ( -> int): {bottom_branch_time} @int: bottom_branch_time
   , ( -> int): {bottom_sig_time} @int: bottom_sig_time
  )
)

{whether to use QR_sig_tables in bottom layer computation}
set QR_code = true

set qrf_sig_tables = make_QRF_sig_tables()

set min_L_bottom_factors(KType mu, int level) = [[(KType, KType)]]:
    let p = parameter(mu)
    then (Q,pL) = theta_stable_quasi_data(p), G = p.real_form
    then muL = LKT(pL)
    then muLjs = factor_K_types(muL), L = Q.Levi
    then Ljs = for muLj in muLjs do muLj.real_form od
     {restriction to each simple factor of L}
    {then Ljs = simple_factors(L) doesn't match real forms of muLjs}
    then DLjs = for muLj@j in muLjs
    	      	do if QR_code
		   then [0]
		   else
		   next_heights(muLj, (semisimple_rank(Ljs[j]) + 1)\2)
		   fi
		od
    then too_many_tauLjs = [[KType]]:
    	 for muLj@j in muLjs
	 do if QR_code
	    then monomials(qrf_sig_tables.branch_entry(muLj))
	    else monomials(branch(muLj, DLjs[j]~[0]))
	    fi
	 od
    then xcL = KGB(L,0)
    then xcLjs = for Lj in Ljs
    	       	 do KGB(Lj, 0)
		 od
    then xcGs = for xcLj in xcLjs do embed_KGB(xcLj,G) od
    then shifts = for xcLj@j in xcLjs {Connects Lj\cap K-types to bottom layer K-types}
    	 	  do K0_param(mu, xcGs[j]).lambda_minus_rho -
		     K0_param(muLjs[j],xcLjs[j]).lambda_minus_rho
		  od
    in for listj@j in too_many_tauLjs
       do let K0 = K_0(xcGs[j])
       	  in for tauLj in listj
       	     do let taujwt = K0_param(tauLj, xcLjs[j]).lambda_minus_rho + shifts[j]
{highest_weight(tauLj, xcLjs[j]).mu + shifts[j]}
	     in if is_dominant(K0, taujwt)
  	        then [(K_types(K_highest_weight_from_K0_weight(xcGs[j], taujwt))[0], tauLj)]
	  	else []
	  	fi
	     od.##
	od


{table of bottom layer data indexed by KType}
set_type
[steady_bottom_data =
( ( -> int) size
  , ( -> ) clear
  , ( -> int) BL_depth
  , (int -> ) adjust_BL_depth
  , (KType->[[(KType,KType)]]) entry
  , (KType->) append
  , (int -> KType) K_type_index
  , (int -> [[(KType,KType)]]) data_index
  , ( -> int) bottom_branch_time
  )
]

set make_steady_bottom_data () = steady_bottom_data:
(  let K_type_hash = make_KType_hash()
   then bottom_data = [[[(KType,KType)]]] : []
   then steps = int:2
   then branch_time = int: 0
   then clear() = void:
       let () = K_type_hash.clear()
       then () = bottom_data:=[]
       then  () = branch_time := 0
       in ()
   in 
   ( ( -> int): { size } @int: K_type_hash.size()
   , ( -> ): {clear} clear
   , ( -> int): {BL_depth} @int: steps
   , (int -> ): {adjust_BL_depth} (int b): steps:= b
   , (KType -> [[(KType,KType)]]): {entry} (KType mu)
     [[(KType,KType)]]:
     begin
	let M = K_type_hash.size()
	then m = K_type_hash.match(mu)
	then () = if m = M
	     	  then let start = elapsed_ms()
		       then () = bottom_data := (bottom_data next bottom_data:= [])
		       	       	 	     #min_L_bottom_factors(mu, steps)
		       in branch_time +:= elapsed_ms() - start
		  fi
	then () = assert(#bottom_data =
	     	K_type_hash.size(),"steady_bottom_data damaged")
	in bottom_data[m]
     end
   , (KType->): {append } (KType mu):
     begin
	let M = K_type_hash.size()
	then m = K_type_hash.match(mu)
	then () = if m = M
	     	  then let start = elapsed_ms()
	     	       then () = bottom_data := (bottom_data next bottom_data:= [])
		       	       	 	     #min_L_bottom_factors(mu, steps)
		       in branch_time +:= elapsed_ms() - start
		  fi
        then () = assert(#bottom_data =
	     	K_type_hash.size(),"steady_bottom_data damaged")
	in ()
     end
   , (int -> KType): {K_type_index} (int j): K_type_hash.index(j)
   , (int -> [[(KType,KType)]]): {data_index} (int j) [[(KType,KType)]]: bottom_data[j]
   , ( -> int): {bottom_branch_time} @int: branch_time
  )
)

{false guarantees not unitary, but true does not guarantee unitary. pairs is (tau, tauL):
a bottom layer K-type and the corresponding L\cap K-type}
set min_bottom_elim(Param p, [(KType,KType)] pairs) = bool:
    let start = elapsed_ms(), (,pL) = theta_stable_quasi_data(p)
    in if  #pairs = 1  then true
       else let DL = max(for (,tauL) in pairs do height(tauL) od),
       	    posLK_time = int:0, multK_time = int:0
       	    then (posL,negL) = K_signature_irrB(first_param(finalize(pL)),DL)
	    	 {positive part of the signature on L}
	    then () = if min_bottom_verbose
	    	      then let () = posLK_time := elapsed_ms()
		      	   in prints("LK_sig used ",
    	    	      	      print_time_string(posLK_time - start))
		      fi
	    then DG = max(for(tau,) in pairs do height(tau) od)
	    then multG = branch_irr(p,DG)
	    	 {all K-mults to height of largest tau}
	    then () = if min_bottom_verbose
	    	      then let () = multK_time := elapsed_ms()
		      	   in prints("multK used ",
    	    	      	      print_time_string(multK_time - posLK_time))
		      fi
		      {create a list of FUNCTIONS returning a bool. Functions
		      are only evaluated until false }
	    then () = if let (, muL) = pairs[0] in posL[muL] = 0
		      then posL := negL
		      fi
	    then answer =  all( for (tau,tauL) in pairs[1:]
       	       	    do @bool: int_part(posL[tauL]) >= int_part(multG[tau])
		    od)
		    {or @bool:int_part...}
    	    then () = if min_bottom_verbose
	       	      then prints("min_bottom_elim used ",
    	    	      	   print_time_string(elapsed_ms() - start))
	       	      fi
            in answer
	fi

{false guarantees not unitary, but true does not guarantee unitary}
{Here P is the K-character of p, at least to heights of pairs}
set min_bottom_elim(Param p, [(KType,KType)] pairs, KTypePol P) = bool:
    let start = elapsed_ms(), (,pL) = theta_stable_quasi_data(p)
    in if #pairs = 1 then true
       else let DL = max(for (,muL) in pairs do height(muL) od){,
       	    posLK_time = int:0, multK_time = int:0}
	    then (posL, negL) = K_signature_irrB(first_param(finalize(pL)),DL)
       	    {then (posL,) = K_signature_irrB(first_param(finalize(pL)),DL)}
	    then () = if let (,muL) = pairs[0] in posL[muL] = 0
		      then posL := negL
		      fi
	    	 {positive part of the signature on L}
	    then DG = max(for(mu,) in pairs do height(mu) od)
	    then multG = P {all K-mults to height of largest mu}
	    in all( for (mu,muL) in pairs[1:]
       	       	    do @:int_part(posL[muL]) >= int_part(multG[mu])
		    od )
	fi


set min_bottom_test(Param p,[(KType,KType)] pairs) = bool:
    if not min_bottom_elim(p, pairs)
    then return false
    else is_unitary(p)
    fi

set min_bottom_test(Param p,[(KType,KType)] pairs, KTypePol P) = bool:
    if not min_bottom_elim(p, pairs, P)
    then return false
    else is_unitary(p)
    fi

set min_bottom_elim(Param p, int D) = bool:
    let start = elapsed_ms()
    then pairs = min_L_bottom(p,D), minLb_time = int:0, posLK_time = int:0,
    	 multK_time = int:0
    then () = if min_bottom_verbose
    	      then let () = minLb_time := elapsed_ms()
    	      	   in  prints(new_line,"min_L_bottom used ",
    	    	       print_time_string(minLb_time - start))
	      fi
    in min_bottom_elim(p, pairs)

set min_bottom_test(Param p, int D) = bool:
    if not min_bottom_elim(p, D)
    then return false
    else is_unitary(p)
    fi

{false guarantees not unitary, but true does not guarantee unitary}
{LKdata[j] is [(tauj,tauLj)]: tauLj is an Lj\cap K-type inducing to a bottom layer tauj of p.}
set min_bottom_elim_factors(Param p, [[(KType,KType)]] LKdata) = bool:
    let start = elapsed_ms(), answers = for k:#LKdata do false od,
    	(,pL) = theta_stable_quasi_data(p)
    then (mu,) = LKdata[0][0]
    then LKTloc = first(for muprime@m in LKTs(p) do muprime=mu od)
    in if LKTloc <0
       then prints("mu isn't a LKT of p"); true
       elif sum(for pairs in LKdata do #pairs od) = #LKdata
       	    then true
       else let pLjs = factor_params(pL)
       	    then DLs = if QR_code
	    	       then null(#LKdata)
		       else for pairs@j in LKdata
	       	       	    do max(for (,tauLj) in pairs
		       	       	   do height(tauLj)
		      	    	   od)
	       	            od
		       fi
      	    then muL = LKTs(pL)[LKTloc]
       	    then muLjs = factor_K_types(muL) {need to line up each muLjs[j] with ONE LKT of pLjs[j]}
       	    then baseLjs = [KType]:
       	    	 for pairs@j in LKdata
	    	 do let (,baseLj) = pairs[0]
	       	    in baseLj
	    	 od
	    then x0js = for muLj@j in muLjs
	    	 	    do muLj.real_form.KGB[0]
			    od
       	    then shiftjs  = for pLj@j in pLjs {for shifting an LKj type to one attached to muLj}
	    	      	    do  let cands = for phiLjk in LKTs(pLj)
			       	   	   do  K0_param(muLjs[j]).lambda_minus_rho -
			       	   	       K0_param(phiLjk).lambda_minus_rho
					   od
				then Lj = pLj.real_form, Lmuj = muLjs[j].real_form
				then coroots = Lj.simple_coroots, zeroj = null(Lj.semisimple_rank)
		      	        then wts = for cand in cands
				     	   do let try = K_highest_weight_from_K0_weight(x0js[j], cand).mu
			   			     {this lifts cand on K0 to X^*}
					      {then () = prints("try = ",try)
					      in (1+x0js[j].involution)*try/2}
					      in try
					   od
				then loc = first(for wt@k in wts
				     	   	 do wt*coroots = zeroj
				     		 od)
				in assert(loc >= 0,"this muLj doesn't match any LKT of pLj"); wts[loc].numer
		      	   od
       	    then qLjs = for pLj@j in pLjs
		      	do let qLj = parameter(baseLjs[j].real_form, (pLj.x).number, pLj.lambda +
		      				    shiftjs[j], pLj.nu)
		           then () = assert(any(for psiLj in LKTs(qLj) do psiLj = baseLjs[j] od),
			   	     		   "qLj fails to have baseLj as a LKT")
			   then () = assert(is_hermitian(qLj),"qLj fails to be hermitian")
			   then () = assert(is_final(qLj),"qLj fails to be final")
			   in qLj
		   	od
            then DG = max(for pairs in LKdata
       	    	     do max(for (tauj,) in pairs
			    do height(tauj)
			    od)
		     od)
       	    then multG = branch_irr(p,DG)
       in all( [(->bool)]: {list of functions, one for each factor Lj}
	       for pairs@j in LKdata
	       do 
	       let (posLj, negLj) = if QR_code
	    	      	    	    then qrf_sig_tables.entry(qLjs[j])
			   	    else K_signature_irrB(qLjs[j],DLs[j])
				  fi
	       then () = if posLj[baseLjs[j]] = 0
		       	 then posLj := negLj
			 fi
	       in @:all( [(->bool)]:
			for(tauj, tauLj) in pairs[1:]
			do {prints(posLj[tauLj]," ",multG[tauj]);}
			@bool: int_part(posLj[tauLj]) >= int_part(multG[tauj])
			od )
	       od )
       fi

set min_bottom_elim_factors(Param p, steady_bottom_data bott_data) = bool:
    all( for mu in LKTs(p)
    	 do min_bottom_elim_factors(p, bott_data.entry(mu))
	 od
       )

{false guarantees not unitary, but true does not guarantee unitary}
{Here P is branch_std(K-character of p to heights of pairs}
{LKdata refers to ONE LKT of p}
set min_bottom_elim_factors(Param p, [[(KType,KType)]] LKdata, KTypePol P) = bool:
    let start = elapsed_ms(), answers = for k:#LKdata do false od,
    	(,pL) = theta_stable_quasi_data(p)
    then (mu,) = LKdata[0][0]
    then LKTloc = first(for muprime@m in LKTs(p) do muprime=mu od)
    in if LKTloc <0
       then prints("mu isn't a LKT of p"); true
       elif sum(for pairs in LKdata do #pairs od) = #LKdata
       	    then true
       else let pLjs = factor_params(pL)
       	    then DLs = for pairs@j in LKdata
	       	       do max(for (,tauLj) in pairs
		       	      do height(tauLj)
		      	      od)
	       	       od
      	    then muL = LKTs(pL)[LKTloc]
       	    then muLjs = factor_K_types(muL) {need to line up each muLjs[j] with ONE LKT of pLjs[j]}
       	    then baseLjs = [KType]:
       	    	 for pairs@j in LKdata
	    	 do let (,baseLj) = pairs[0]
	       	    in baseLj
	    	 od
       	    then shiftjs  = for pLj@j in pLjs {for shifting an LKj type to one attached to muLj}
	    	      	    do  let cands = for phiLjk in LKTs(pLj)
			       	   	   do  K0_param(muLjs[j]).lambda_minus_rho -
			       	   	       K0_param(phiLjk).lambda_minus_rho
					   od
				then Lj = pLj.real_form, Lmuj = muLjs[j].real_form
				then coroots = Lj.simple_coroots, zeroj = null(Lj.semisimple_rank)
		      	        then wts = for cand in cands
				     	   do K_highest_weight_from_K0_weight(
					       	    Lmuj.KGB[0], cand).mu
			   			     {this lifts cand on K0 to X^*}
					   od
				then loc = first(for wt@k in wts
				     	   	 do wt*coroots = zeroj
				     		 od)
				in assert(loc >= 0,"this muLj doesn't match any LKT of pLj"); wts[loc]
		      	   od
       	    then qLjs = for pLj@j in pLjs
		      	do let qLj = parameter(baseLjs[j].real_form, (pLj.x).number, pLj.lambda +
		      				    shiftjs[j], pLj.nu)
		           then () = assert(any(for psiLj in LKTs(qLj) do psiLj = baseLjs[j] od),
			   	     		   "qLj fails to have baseLj as a LKT")
			   in qLj
		   	od
            in all( [(->bool)]: {list of functions, one for each factor Lj}
       	    	for pairs@j in LKdata
       	    	do if #pairs = 1 then @: true
		   else {let (,tauj0) = pairs[0]}
		   	let qLj = qLjs[j]
		   	{qLj is now a Param for the same real form as tauj}
			then (posLj,negLj) = K_signature_irrB(qLjs[j], DLs[j])
	       	  	then () = if posLj[baseLjs[j]] = 0
		       	     	  then posLj := negLj
			    	  fi
	    	      	{positive part of the signature on Lj}
	           	in @:all( [(->bool)]:
	       	  	     for (tauj,tauLj) in pairs[1:]
       	       	    	     do @bool: int_part(posLj[tauLj]) >= int_part(P[tauj])
		             od )
		    fi
	        od )
	fi

set min_bottom_elim_factors(Param p, steady_bottom_data bott_data, KTypePol P) = bool:
    all( for mu in LKTs(p)
    	 do min_bottom_elim_factors(p, bott_data.entry(mu), P)
	 od
       )
set min_bottom_test_factors(Param p, [[(KType,KType)]] LKdata) = bool:
    if not min_bottom_elim_factors(p, LKdata)
    then return false
    else is_unitary(p)
    fi

set min_bottom_test_factors(Param p,[[(KType,KType)]] LKdata, KTypePol P) = bool:
    if not min_bottom_elim_factors(p, LKdata, P)
    then return false
    else is_unitary(p)
    fi

set min_bottom_test_factors(Param p, steady_bottom_data bott_data) = bool:
    if not min_bottom_elim_factors(p, bott_data)
    then return false
    else is_unitary(p)
    fi

set min_bottom_test_factors(Param p, steady_bottom_data bott_data, KTypePol P) = bool:
    if not min_bottom_elim_factors(p, bott_data, P)
    then return false
    else is_unitary(p)
    fi

set min_bottom_elim_factors(Param p, int level) = bool:
    let start = elapsed_ms()
    then LKdatas = for mu in LKTs(p) do min_L_bottom_factors(mu,level) od
    	 , minLb_time = int:0, posLK_time = int:0, multK_time = int:0
    then () = if min_bottom_verbose
    	      then let () = minLb_time := elapsed_ms()
    	      	   in  prints(new_line,"min_L_bottom used ",
    	    	       print_time_string(minLb_time - start))
	      fi
    in all( [(->bool)]: for LKdata in LKdatas do @bool: min_bottom_elim_factors(
       	    	   p, LKdata)
       	    od)

set min_bottom_test_factors(Param p, int level) = bool:
    if not min_bottom_elim_factors(p, level)
    then return false
    else is_unitary(p)
    fi
