{ inversion of a unitriangular poly_mat }

<basic.at
<polynomial.at

{ add c times column i to column j in matrix A of size n*n (changes A[i][j]) }
set column_operation (i_poly_mat A, int i, int j, i_poly c, int n) = i_poly_mat:
  for k:n { generate row k of result }
  do let row=A[k] in
    for l:n do if l=j then row[l]+poly_product(c,row[i]) else row[l] fi od
  od

{ add c times row j to row i in matrix A of size n*n (changes A[i][j]) }
set row_operation (i_poly_mat A, int i, int j, i_poly c, int n) = i_poly_mat:
  let source=A[j], dest=A[i] in
  A[i] := for l:n do dest[l]+poly_product(c,source[l]) od; A

{ find first non-zero entry above diagonal, starting at lower right }
set find_nonzero (i_poly_mat M) = (int,int):
  let rv=(-1,-1),n=#M,done=false
  then i=n-2,j=n-1
  in
  while not done
  do
    if i<0 then done:=true { nothing found, return (-1,-1) }
    else let entry=strip(M[i][j]) in
      if !=entry then rv:=(i,j); done:=true { found, return location }
      elif (j+:=1)=n then j:=(i:=i-1)+1 { advance in row, wrap back at end }
      fi
    fi
  od; rv

{ internal function: for square matrices A,B of size n, compute B*A^{-1} }
set inv_p (i_poly_mat A, i_poly_mat B, int n) = i_poly_mat:
  let id=identity_poly_matrix(n), i_p(i_poly_mat M,i_poly_mat N) = i_poly_mat: [] in
  begin i_p:=
    ( (i_poly_mat M,i_poly_mat N):
    { let ()=prints("M:"); printPolyMatrix(M) in
      let ()=prints("N:"); printPolyMatrix(N) in
    }
      let (i,j)=find_nonzero(M) in
      if i<0 then N { we've found the result }
      else print(i,j); let p=M[i][j] in
          i_p(column_operation(M,i,j,-p,n),column_operation(N,i,j,-p,n))
      fi
   )
   end { end of assignment, now apply result } (A,B)

{ inverse of an upper triangular polynomial matrix }
set inverse (i_poly_mat M) = i_poly_mat:
begin
  let n=#M in
  assert(all(n,(int i) bool: let Mi=M[i] in all(i,(int j) bool: =Mi[j]))
        ,"Matrix isn't upper triangular")
; assert(all(n,(int i) bool: M[i][i]=[1])
        ,"Matrix doesn't have ones on diagonal")
; inv_p(M,identity_poly_matrix(n),n)
end

set unitri_inv (i_poly_mat M) = i_poly_mat:
begin
  let n=#M in
  assert(all(n,(int i) bool: let Mi=M[i] in all(i,(int j) bool: =Mi[j]))
        ,"Matrix isn't upper triangular")
; assert(all(n,(int i) bool: =(M[i][i]-[1]))
        ,"Matrix doesn't have ones on diagonal")
; for i:n { compute row i of inverse }
  do let row = M[i] in
    for j:n { compute entry j of that row, and update row }
    do
      if j<=i then if j<i then poly_0 else poly_1 fi
      else let coef=row[j] in
        if !=coef { avoid work for zero coefficients }
        then let use_row=M[j] in { update the linear comb. of rows: }
          row:= for l:n do row[l]-poly_product(coef,use_row[l]) od
        fi
      ; -coef { this goes into position [i][j] of the result }
      fi
    od
  od
end

set regular_KL_matrix (Param p) = i_poly_mat:
  let (B,,ind,polys,,,)=KL_block(p) then (r,c)=shape(ind) in
  for i:r do for j:c do polys[ind[i,j]] od od
