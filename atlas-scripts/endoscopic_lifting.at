{For now: G, H split; no issue with rho_G-rho_H cover
also: only pure strong real forms}
<L_hom.at

set e_verbose=false

{M: X^*(H_v) to X^*(G_v), i.e. also takes X_*(H) to X_*(G)}

set endoscopic_lift(L_homomorphism phi_H,RealForm G,mat M)=
phi(map_KGB_gen_tits(phi_H.y,dual_quasisplit_form(G),M),phi_H.gamma*M)

{x_H must be a pure KGB element for H}
{long version includes p_G}
set epstar_std_long(KGBElt x_H,L_homomorphism phi_H,RealForm G,mat M)=(Param,ParamPol):
assert(is_pure(x_H), "x_H is not pure (epstar only defined at the moment for pure real forms of H and G");
let y_H=phi_H.y then
gamma_H=phi_H.gamma then
()=if e_verbose then prints("y_H: ", y_H, new_line, "gamma_H: ", gamma_H) fi  then
gamma_G=M*gamma_H then
()=if e_verbose then prints("gamma_G: ", gamma_G) fi  then
y_G=map_KGB_gen_tits(y_H,dual_quasisplit_form(G),M) then
()=if e_verbose then prints("y_G: ", y_G) fi then
phi_G_0=phi(y_G,gamma_G) then
()=assert(is_valid(phi_G_0), "phi_G is not valid") then
theta_G_v=involution(y_G) then
theta_G=-^theta_G_v then
{()=if e_verbose then prints("theta_G: ", theta_G,new_line,"torus_factor(x_H): ", torus_factor(x_H)) fi then
mu_H=unnormalized_torus_factor(x_H) then
()=if e_verbose then prints("unnormalized torus factor of x_H: ", mu_H) fi then
mu_G=mu_H*M+ rho_check(G)  then
()=if e_verbose then prints("unnormalized torus factor of x_G: ", mu_G) fi then
tau_G=(1+theta_G)*(2*mu_G-rho_check(G))/2 then
()=if e_verbose then prints("normalized torus factor of x_G: ", tau_G) fi then}
{x_G=KGB_elt(inner_class(G),theta_G,tau_G) then}
x_G_0=KGB_elt(inner_class(G),theta_G,x_H.torus_factor*M) then
()=if e_verbose then prints("x_G_0: ", x_G_0, " ", x_G_0.real_form, " ", x_G_0.torus_factor) fi then
(x_G,phi_G)=make_dominant(x_G_0,phi_G_0) then
()=if e_verbose then prints("x_G: ", x_G, " ", x_G.real_form, " ", x_G.torus_factor) fi then
p_G=parameter(x_G,phi_G_0) then
()=if e_verbose then prints("p_G=", p_G) fi
in (p_G,finalize(p_G))

set epstar_std(KGBElt x_H,L_homomorphism phi_H,RealForm G,mat M)=ParamPol:let (,X)=epstar_std_long(x_H,phi_H,G,M) in X

{assume M=id}
set epstar_std(KGBElt x_H,L_homomorphism phi_H,RealForm G)=ParamPol:let (,X)=epstar_std_long(x_H,phi_H,G,id_mat(x_H.root_datum.rank)) in X

set epstar_std(Param p_H,RealForm G,mat M)=ParamPol:
let gamma_H=p_H.infinitesimal_character then
y_H=y_gen(p_H) then
phi_H=phi(y_H,gamma_H) then
x_H=p_H.x in
epstar_std(x_H,phi_H,G,M)

{assume M=id}
set epstar_std(Param p_H,RealForm G)=ParamPol:epstar_std(p_H,G,id_mat(G.rank))

set epstar_std(ParamPol P_H,RealForm G,mat M)=ParamPol:
sum(G,for c@p in P_H do c*epstar_std(p,G,M) od)

{assume M=id}
set epstar_std(ParamPol P_H,RealForm G)=ParamPol:epstar_std(P_H,G,id_mat(rank(G)))

set epstar(L_homomorphism phi_H,RealForm G,mat M)=L_homomorphism:
let y_H=phi_H.y then
gamma_H=phi_H.gamma then
()=if e_verbose then prints("y_H: ", y_H, new_line, "gamma_H: ", gamma_H) fi  then
gamma_G=M*gamma_H then
()=if e_verbose then prints("gamma_G: ", gamma_G) fi  then
y_G=map_KGB_gen_tits(y_H,dual_quasisplit_form(G),M) then
()=if e_verbose then prints("y_G: ", y_G) fi then
phi_G=phi(y_G,gamma_G) in
assert(is_valid(phi_G),"phi_G is not valid");phi_G

{assume M=id}
set epstar(L_homomorphism phi_H,RealForm G)=L_homomorphism:epstar(phi_H,G,id_mat(G.rank))


{for debugging
 usage: set (y_H,y_G,gamma_H,gamma_G,phi_G,theta_G,x_G,p_G) =epstar_std_info(x_H,phi_H,G,M)a
}
set epstar_std_info(KGBElt x_H,L_homomorphism phi_H,RealForm G,mat M)=(KGBElt_gen,KGBElt_gen,ratvec,ratvec,L_homomorphism,mat,KGBElt,ParamPol):
assert(is_pure(x_H), "x_H is not pure (epstar only defined at the moment for pure real forms of H and G");
let y_H=phi_H.y then
gamma_H=phi_H.gamma then
()=if e_verbose then prints("y_H: ", y_H, new_line, "gamma_H: ", gamma_H) fi  then
gamma_G=M*gamma_H then
()=if e_verbose then prints("gamma_G: ", gamma_G) fi  then
y_G=map_KGB_gen_tits(y_H,dual_quasisplit_form(G),M) then
()=if e_verbose then prints("y_G: ", y_G) fi then
phi_G=phi(y_G,gamma_G) then 
theta_G_v=involution(y_G) then
theta_G=-^theta_G_v then
()=if e_verbose then prints("theta_G: ", theta_G,new_line,"torus_factor(x_H): ", torus_factor(x_H)) fi then 
x_G=KGB_elt(inner_class(G),theta_G,torus_factor(x_H)*M) then
()=if e_verbose then prints("x_G: ", x_G, " ", x_G.real_form, " ", x_G.torus_factor) fi then
p_G=parameter(x_G,phi_G) then
()=if e_verbose then ("p_G=", p_G) fi
in (y_H,y_G,gamma_H,gamma_G,phi_G,theta_G,x_G,finalize(p_G))

set epstar_std_info(Param p_H,RealForm G,mat M)=(KGBElt_gen,KGBElt_gen,ratvec,ratvec,L_homomorphism,mat,KGBElt,ParamPol):
let gamma_H=p_H.infinitesimal_character then
y_H=y_gen(p_H) then
phi_H=phi(y_H,gamma_H) then
x_H=p_H.x in
epstar_std_info(x_H,phi_H,G,M)

{testing: given G, H, compute endoscopic lifting of all unipotent packets from H to (pure real forms of) G
sample usage:
set lifts=test_epstar_std(Sp(6,R),[1,1,1]/2)

to lift from H=compact torus, make s=rho_check(G)/Coxeter number, add long Weyl group element
set G=Sp(6,R)
set lifts=test_epstar_std(G,rho_check(G)/coxeter_number(G),w0(G))
}

set test_epstar_std(RealForm G,ratvec v,WeylElt w)=
let (H,M,Hd)=endoscopic_group(G,v,w) then
unip_H=unipotent_representations(H) then
lifts=##for ph@i in unip_H do
let ()=if e_verbose then prints("i=",i) fi then
 gamma_H=ph.infinitesimal_character then
 gamma_G=gamma_H*M then
 y=y_gen(ph) then
 y_G=map_KGB_gen_tits(y,dual_quasisplit_form(G),M) then
 phi_G=phi(y_G,gamma_G)  then
 phi_H=phi(y,gamma_H) then
 S_H=pure_component_group(phi_H)
 in for x in S_H do epstar_std(x,phi_H,G,M) od
od
in prints("G=",G,new_line,"s=",v,new_line,"H=",H);
prints("#lifts: ", #lifts);lifts

set test_epstar_std(RealForm G,ratvec v)=test_epstar_std(G,v,id_W(G))

{---------------irreducibles-----------}



