<basic.at
<extended.at { for E@(mat,KType) }
<induction.at { for dim_u_cap_p@KGBElt }

{ commands for converting from c-form to hermitian-form}
{ Compute (lambda+rho) restricted to torus T }

{ lambda\in X^*+rho =>
    one can define a parameter with this lambda, as p=param(x,lambda-rho,0)
  then infinitesimal_character(p)=d_lambda(p)
  which is lambda restricted to T-tilde
  apply this with lambda itself returns (1+theta)*lambda/2  (on T-tilde)
  apply this with lambda=rho,   returns (1+theta)*rho/2     (on T-tilde)
  then the sum is (1+theta)(lambda+rho)/2 on T
}
set lambda_plus_rho_res_T (KGBElt x,ratvec lambda) = ratvec:
  (1+x.involution)*(lambda+rho(x.root_datum))/2

{ mu(p) is the value of the lowest K-type of p, evaluated at x(p) }
{ formula: mu(p) = < torus_factor(x)+rho^\vee , (1+theta_x)(lambda+rho)/2 > }
set mu (Param p) = rat:
{ Since |p.x.torus_factor| is unchanged under right multiplication by
  |1+x.involution|, the following formula is equivalent to the original one:
  (p.x.torus_factor+p.real_form.rho_check) * lambda_plus_rho_res_T(p.x,p.lambda)
}
  p.real_form.rho_check * lambda_plus_rho_res_T(p.x,p.lambda) +
  p.x.torus_factor*(p.lambda+p.root_datum.rho)

{ In fact it only depends on the K-type. Here we use the original formula }
set mu (KType t) = rat:
  (t.x.torus_factor+t.root_datum.rho_check) *
  ((1+t.x.involution)
  *(t.lambda_minus_rho+two_rho(t.root_datum))
  ) / 2

{  converting to Hermitian forms in unequal rank case  }

{ assumption: theta=involution(x) commutes with delta and x has no complex
  descents (this holds for x=x(p) when p is finalized parameter with nu(p)=0)
  Then the set of positive real roots is delta-invariant; the following
  function computes the number of unordered pairs $P=\{ alpha,delta(alpha) \}$
  with alpha a positive real real and < alpha^vee , delta*alpha > = 0,
  whose parity gives sign of delta acting on Wedge^top(positive real roots).
  Each orthogonal pair $P$ contributes $-1$ to sign, others contribute nothing:
    if <alpha^vee,delta*alpha> = -1, contributes 1 by computation in SL(3,R):
      -1 from pair is cancelled by -1 from action on
      [X_alpha,X_delta(alpha)=X_{alpha+delta(alpha)}
    if delta(alpha)=alpha, contributes 1 by reduction to simple case,
  note: every really-simple root (simple among real roots) is actually simple
}
set number_real_orthogonal_pairs (KGBElt x, mat delta) = int:
  let rd=x.root_datum, theta=x.involution in
  assert(delta*theta=theta*delta
        ,"Cartan involution does not commute with delta");
  assert(no_Cminus_roots(x)
        , "Not defined unless all complex roots are type C+");
  for alpha in real_posroots(rd,theta)
  do int:
    let pairing=coroot(rd,alpha)*delta*alpha in
    assert(=pairing or pairing=-1 or pairing=2 { since we paired \emph{roots} }
          ,"invalid pairing of root and coroot");
    #=pairing { count zero pairings }
  od.sum.half { compensate for each pair being counted twice }

{ function |mu| below was initially described in Vogan in email of Oct 25, 2016
  with subject: mu(p); it has been significantly modified since
  It is a sum of rational terms, for which eventually differences (which should
  be integer) will serve as exponents of |s|, therefore interpreted modulo 2
  To facilitate analysis of the terms |mu_terms| returns them as a triple
}

set mu_terms  (KType tp,mat delta) = (rat,rat,int):
  let E=E(delta,tp), x=x(tp) then g_l=E.g-E.l
  in
  ( { lambda_rho_term = } (g_l-rho_check_r(x))*(tp.lambda_rho)
  , { tau_term        = } (E.l*(delta-1)*E.tau)/2
  , { dim_term        = } dim_u_cap_p(x)
  ) { the sum of these will be interpreted modulo 2/1 }

set mu (KType tp,mat delta) = rat: let (l,t,d) = mu_terms(tp,delta) in l+t+d

set print_mu (KType tp) = void:
     let (lt,tt,dt)=mu_terms(tp,tp.real_form.distinguished_involution) in
     prints(tp, "    ", 1 , "     ", "  ", lt, "  ", tt, "     ", dt, lt+tt+dt )

{.convert (twisted) c-form to hermitian form, using p0 for normalization.}
set convert_cform_hermitian (KTypePol P,mat delta, KType p0)= KTypePol:
  if =#P then P { since we cannot select a term from a null polynomial }
  else let a_mu = mu(p0,delta) in
    P.null_K_module +
      for c@p in P do (c*(mu(p,delta)-a_mu).rat_as_int.exp_s,p) od
  fi

{ if no |p0| provided, use |first_param(P)|, a term of least height in |P| }
set convert_cform_hermitian (KTypePol P,mat delta) = KTypePol:
  convert_cform_hermitian(P,delta,first_K_type(P))

{.version that defaults delta to inner class involution.}
set convert_cform_hermitian (KTypePol P,KType p0)= KTypePol:
  convert_cform_hermitian(P,P.real_form.distinguished_involution,p0)

{ apply both defaults simultaneously; this one is used from hermitian.at }
set convert_cform_hermitian (KTypePol P)= KTypePol:
  convert_cform_hermitian(P,P.real_form.distinguished_involution,first_K_type(P))
