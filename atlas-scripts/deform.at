<basic.at
<polynomial.at { for type |poly| and function |eval| }

{ this file defines commands that are equivalent to the |full_deform| command
  that is now built-in; it therefore serves mainly documentation (and
  historic) purposes, and should not be required for any other scripts. In
  fact we added some initial commands that mimick the methods |KL_sum_at_s|
  and |deformation_terms| that were already originally implemented directly in
  the library, and used through the |deform| atlas command, in order to show
  after the fact how this could have been done as an atlas script
}

set compress { output from |partial_KL_block| to a more useful form } (
    [Param] B, mat KL_mat, [poly] polys, vec stops, vec survivors, mat map) =
  ([Param], mat, [int]): { $(block,((-1)^l(y/s)*P_{x,y}[q:=-1],lengths)$ }
  let polys_at_minus_1 = for p in polys do eval(p,minus_1) od
  then KL_mat_at_minus_1 = mat: { matrix of polynomial evaluations at -1 }
    for col in KL_mat do for inx in col do polys_at_minus_1[inx] od od
  then reduced_KL_mat = mat: for j in survivors do map*KL_mat_at_minus_1[j] od
  , survivor_lengths = let l=0 in
    for s in survivors do while stops[l+1]<=s do l+:=1 od; l od
  in
  ( for i in survivors do B[i] od { block restricted to survivors }
  , { matrix of associated P polynomials, but evaluated at -1 and sign-twisted }
    for col@j in reduced_KL_mat
    do (vec:for e@i in col do minus_1^survivor_lengths[i]*e od)
       *minus_1^survivor_lengths[j]
    od
  , survivor_lengths { length mapping }
  )

set deformation_terms (Param p) = ParamPol:
  let (B,KL_mat,length) = compress(partial_KL_block(p)) then nB=#B in
  if =nB or B~[0]!=p
  then null_module(p) { if |p| itself is not among survivors, return 0 }
  else { compute final column |acc| of |Q| matrix by solving linear system
         $P'*acc=e_p$ with $P'$ a matrix of Split, columns from |KL_sum_at_s| }
    let remainder = null(nB-1)#1 { start with standard basis vector for |p| }
    , solution=null(nB)
    , opposite_parity=1-length~[0]%2 { opposite to that of |length(p)| }
    , !one_minus_s = 1-s, !on_p = orientation_nr(p)
    in
    for q_ind : nB
    ~do let contrib=remainder[q_ind]*KL_mat[q_ind] in
      remainder -:= contrib
    ; if length[q_ind]%2=opposite_parity
      then solution +:= contrib
      fi
    od
  ; null_module(p)+
    for c@i in solution
    do { twist (1-s)*solution according to orientation numbers of its terms }
      let x=B[i] then on_x=orientation_nr(x) then (q,r)=(on_p-on_x)\%2 in
      assert(=r,"odd orientation");
      (minus_1^q*c*one_minus_s,x) { we have |s^q*c*(1-s) = (-1)^q*c(1-s)| }
      { for the same reason we replaced |s| by |-1| in computations giving |c| }
    od
  fi

{ the original version of this file started here }

set has_nu0 (Param p) = bool: =nu(p)
set d_verbose=false

{ if at reducibility point stay there, otherwise go to next lower one }
set weak_lower (Param p) = Param:
  let (x,lambda,gamma)=%p, rp=reducibility_points(p)
  then lim = #rp
  in param(x,lambda,if lim=0 then gamma*0 else gamma*rp[lim-1] fi)

{ go to next reducibility point (move even if at reducibility point) }
set lower (Param p) = Param:
  let (x,lambda,gamma)=%p, rp=reducibility_points(p)
  then lim = let nrp=#rp in if nrp>0 and rp[nrp-1]=1/1 then nrp-1 else nrp fi
  in  param(x,lambda,if lim=0 then gamma*0 else gamma*rp[lim-1] fi)

set deformation (Param p) = (Param,ParamPol): { deformed Param and new terms }
  (lower(p),deform(p)) { here |deform| may be replaced by |deformation_terms| }

set recursive_deform (Param p) = ParamPol:
  if d_verbose then prints("recursive_deform with:", p) fi;
  if is_zero(p) or not is_final(p) then error("improper parameter") fi;
  let empty=null_module(real_form(p)) in
  if d_verbose { choose verbose or non-verbose version of the function }
  then rec_fun full_def (Split sc,Param p) ParamPol:
			 { |sc| will simply scale the resulting ParamPol }
    begin
      prints("full_def with: sc=", sc, ", p=", p);
      if has_nu0(p) then sc*p
      else
	assert(is_standard(p)
	      ,"non standard parameter encountered in deformation");
	let rp=reducibility_points(p)
	then inx=#rp, acc=empty
	, at_nu0=ParamPol: sc*(p*0) { fully deformed |p| }
	in
        while inx>0
        do
	  let p_def=p*rp[inx-:=1] { next deformation of |p| }
	  then def=deformation_terms(p_def) { bypass built-in |deform| here }
          in
	  prints("adding deformation term: p=", p_def, new_line,"adding:", def)
	; acc+:=def
        od
      ; for k@q in acc { traverse the sum of the deformation terms spun off }
        do { apply |full_def| recursively to all those terms }
	  at_nu0 +:= full_def(sc*k,q) { gather harvest from those deformations }
	od
      ; at_nu0
      fi
    end
  else rec_fun full_def (Split sc,Param p) ParamPol:
			 { |sc| will simply scale the resulting ParamPol }
    begin
      if has_nu0(p) then sc*p
      else
	assert(is_standard(p)
	      ,"non standard parameter encountered in deformation");
	let rp=reducibility_points(p)
	then inx=#rp, acc=empty
	, at_nu0=ParamPol: sc*(p*0) { fully deformed |p| }
	in
        while inx>0
        do
	  let p_def=p*rp[inx-:=1] { next deformation of |p| }
	  then def=deformation_terms(p_def) { bypass built-in |deform| here }
          in acc+:=def
        od
      ; for k@q in acc { traverse the sum of the deformation terms spun off }
        do { apply |full_def| recursively to all those terms }
	  at_nu0 +:= full_def(sc*k,q) { gather harvest from those deformations }
	od
      ; at_nu0
      fi
    end
  fi { apply that recursive function to the initial argument: } (1,p)
