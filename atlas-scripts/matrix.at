<basic.at { for |sum@[int] }

{ Functions for some non elementary but general matrix operations, like finding
  a left or right inverse of a given matrix, if one exists.
}


set upper_unitriangular_inverse (mat U) = mat:
   let n=#U then Q=id_mat(n) { is made to satisfy $Q*U=I_n$ below }
in for Uj@j in U
   do Q[j] := for i:j do -sum(for k:j-i from i do Q[i,k]*Uj[k] od) od
     ## Q[j][j:] { retain the final $n-j$ rows of column $j$ of $Q$ }
   od; Q

{
  The function |ech_up| is a complement to the built-in |echelon@mat| (which
  produces an "upper" column echelon form by column operations) that, in case
  some pivots are below the main diagonal, complements this with row operations
  to make the result truly "upper" (meaning all sub-diagonal entries are zero).

  Make (assumed) result of |echelon| upper triangular using row operations,
  possibly with trailing null rows; return the resulting triangular matrix, and
  the row operations matrix (the one left-multiplied by to obtain it)
}
set ech_up (mat M, [int] pivot) = (mat,mat):
{ assumptions: |pivot| is strictly increasing list of pivot row numbers for |M|
  and one has |M[i,j]=0| whenever |i>pivot[j]

  Since |Bezout| thinks of its argument as a row matrix, we transpose internally
}
   let (k,l)=shape(M:=^M)
   then C = id_mat(l) { record column operations on the l-column transposed M }
in for i:k { loop over rows of |M|, which get modified by column operations }
   do let jj=pivot[i]+1 { column of position beyond the pivot in row i }
   in if i<pivot[i] { avoid doing any work when our range is a single column }
      then
         let (d,c_ops) = { gcd of post-diagonal part of row i & colmn ops }
           Bezout(vector(jj-i,(int j):M[i,i+j])) { a short non-empty range }
         then new_M_cols = M[i:jj] * c_ops, new_C_cols = C[i:jj] * c_ops
      in assert(all(jj-i,(int j): new_M_cols[i,j]=if j=0 then d else 0 fi))
      ; for j:jj-i do M[i+j]:=new_M_cols[j]; C[i+j]:=new_C_cols[j] od
      fi
   od; (^M,^C) { transpose back on exit }

set left_inverse(mat A) = mat: { for injective map to saturated sublattice }
(  let (M,C,pivots,)=echelon(A), c=#A
in if #M<c then error("not injective (rank is ",#M,"<",c,")") fi
;  let (Tri,R)= ech_up(M,pivots)
in if let diag=Tri.main_diagonal in diag=ones(#diag)
   then C*upper_unitriangular_inverse(Tri[:c,:])*R[:c,:]
   else error("image is not saturated")
   fi
)

set right_inverse(mat A) = mat: { section of a surjective lattice map A }
(  let (M,C,pivots,)=echelon(^A), c=A.n_rows
in if M.n_columns<c then error("row rank ",M.n_columns," not full (",c,")") fi
;  let (Tri,R)= ech_up(M,pivots)
in if let diag=Tri.main_diagonal in diag=ones(#diag)
   then ^(C*upper_unitriangular_inverse(Tri[:c,:])*R[:c,:])
   else error("matrix is not surjective")
   fi
)

set weak_left_inverse(mat A) = (mat,int): { left inverse after saturation }
(  let (M,C,pivots,)=echelon(A), c=#A
in if #M<c then error("not injective (rank is ",#M,"<",c,")") fi
;  let (Tri,R)= ech_up(M,pivots) then (T1,d)=invert(Tri[:c,:])
in ( C*T1*R[:c,:] , d)
)

set factor_scalar (mat M) = { factor out common divisor } (int,mat):
  let d=gcd(for col in M do gcd(col) od) in (d,if =d then M else M\d fi)

set principal_submatrix(mat M, [int] S)=mat: { choice |S| of rows, columns }
   let m=#S in matrix((m,m), (int i,int j)int: M[S[i],S[j]] )

set main_diagonal_square_block(mat M, int size, int offset) = mat:
  M[ offset:offset+size, offset:offset+size ]
set top_left_square_block(mat M,int size) = mat: M[ :size, :size]
set leading_principal_minor(mat M,int size)=mat:top_left_square_block(M,size)

{select/first_rows/columns are defined in lattice.at}

{row echelon form, just the mat itself, see builtin |echelon|}
set row_echelon(mat M)=mat:let (E,,,)=echelon(^M) in ^E

{concatenate matrices with same number of rows}
set merge_matrices ([mat] matrices) = mat:
if #matrices=0 then id_mat(0) else ##(n_rows(matrices[0]),matrices) fi

set string_compact (mat M) = string:
   let size = 0.max { of all entry widths } (for e in  ##M do #(e.to_string) od)
in for row in ^M
   do new_line + for x in row do pad(x.to_string,size+1) od.##
   od.##

set print_matrix_compact(mat M)=prints(string_compact(M))

{add an integer to one entry of a matrix; language already provides this}
set add_to_matrix_entry(mat M, int i, int j, int value)= mat:
  M[i,j]+:=value; M


{A: (m,n) matrix, m rows, n columns
 B: (p,q) matrix  p rows, q columns
recall:
 in atlas A[i] is the i^th column
 A[i][j]: j^th row, i^th column
 A[i,j]:  i^th row, j^th column (the usual)

So
A=A[i,j] 0<=i<=p-1, 0<=j<=q-1
B=B[k,l] 0<=k<=m-1, 0<=l<=n-1
let
K=A\otimes B of size (mp,nq)
so
K=K[x,y] with 0<= x <= mp-1, 0<= y <= nq-1

K[pr+v,qs+w]=A[r,s]*B[v,w]
where r<=m-1, v<=p-1 => pr+v<=p(m-1)+(p-1)=pm-1
where s<=n-1, w<=q-1 => qs+w<=q(n-1)+(q-1)=qn-1
inverting:
K[x,y]=A[r,s]*B[v,w] where
x=pr+v,r<=m-1, v<=p-1 => r=x\p, v=x%p
y=qs+w,s<=n-1, w<=q-1 => r=y\q, w=y%q

Conclusion:

K[x,y]=A[x\p,y\q]*B[x%p,y%q]  (x<=mp-1, y<=nq-1)

}

set kronecker_product(mat A,mat B)=mat:
let (m,n)=shape(A), {m rows, n columns}
(p,q)=shape(B) in   {p rows, q columns}
for y:n*q do
 for x:m*p do
{  prints("x:",x, " y:",y," x\p:",x\p, " y\q:",y\q, " x%p:",x%p, " y%q:",y%q);}
  A[x\p,y\q]*B[x%p,y%q] od od  {mp rows, nq columns}

set Kronecker_product(mat A,mat B)=mat:kronecker_product(A,B)

{tools to keep track of the indices}
{Given (m,n) and (p,q)
 X={(x,y) | x<=mp-1, y<=nq-1}
 Y={(r,s,v,w) | r<=m-1, s<=n-1 v<=p-1, w<=q-1}
 (all indices are >=0)
 kronecker_f: X -> Y
 kronecker_g: Y -> X
are inverse bijections}

set kronecker_f(int m, int n, int p, int q)=((int, int)->(int,int,int,int)):
(int x,int y):
assert(0 <= x and x<= m*p-1 and 0<= y and y<= n*q-1,"index out of range");
(x\p,y\q,x%p,y%q)

set kronecker_g(int m, int n, int p, int q)=((int,int, int, int)->(int,int)):
(int r, int s, int v, int w):
assert(0 <= r and r<= p-1 and 0<= s and s<= q-1,"index out of range");
(p*r+v,q*s+w)
