<Weylgroup.at { dominant }

{ finite dimensional representations of G(C)}

{.constructs parameter for the algebraic finite-dimensional of
  extremal weight lambda.}
set finite_dimensional(RealForm G, vec lambda) = Param:
  let lambda_dom = dominant(G,lambda)
  in  param(x_open(G),lambda_dom,lambda_dom+G.rho)

{.whether p is finite dimensional.}
set is_finite_dimensional (Param p) = bool:
   #tau(p) = p.real_form.semisimple_rank
   { this implies the |p| has full integrality rank }

{.for protecting functions intended for finite dimensional parameters only.}
set fd_only (Param p) = Param:
  assert(@:is_finite_dimensional(p),"representation is infinite dimensional"); p

{.dimension of finite dimensional representation from its highest weight.}
set dimension (RootDatum rd, ratvec lambda) = int:
( let lambda_dom=dominant(rd,lambda) in
  if lambda != lambda_dom
  then prints("Warning: changed ", lambda, ", to ", lambda_dom
             , " to make it dominant")
  ;  lambda := lambda_dom
  fi
; let rho=rho(rd) in { now apply Weyl's dimension formula }
  product(for a in poscoroots(rd) do (lambda+rho)*a/(rho*a) od).rat_as_int
)

set Weyl_dim (RootDatum rd, vec lambda) = int:
( lambda:=dominant(rd,lambda)
; let rho=rho(rd) in { now apply Weyl's dimension formula }
  for a in poscoroots(rd) do (lambda+rho)*a/(rho*a) od.product.rat_as_int
)

{ If split torus in the center not necessarily a vec, this could fail.
  Leave it for now because otherwise weyl_character_formula.at fails to load }
{.highest weight of finite dimensional in X^* coordinates.}
set highest_weight_finite_dimensional (Param p) = vec:
  (p.fd_only.dominant.infinitesimal_character - p.root_datum.rho).ratvec_as_vec

{allow ratvec if G has split central torus}
set highest_weight_finite_dimensional_ratvec (Param p) = ratvec:
   (p.fd_only.dominant.infinitesimal_character - p.root_datum.rho)

set on_fundamental_weights (ratvec v,RootDatum rd) = vec:
   (v*simple_coroots(rd)).ratvec_as_vec

{.fundamental weight coordinates of finite dimensional parameter.}
set fundamental_weight_coordinates (Param p) = vec:
   on_fundamental_weights(highest_weight_finite_dimensional(p),root_datum(p))

{use highest_weight_finite_dimensional_ratvec to all split central torus}
{.dimension of finite dimensional parameter.}
set dimension(Param p) = int:
   dimension(root_datum(p),highest_weight_finite_dimensional_ratvec(p))


{ this function depends on a complex root datum, with half-swapping involution }
set complex_LKT (Param p) = vec:
  let dom = dominant(root_datum(p), ((1+p.x.involution)*p.lambda).ratvec_as_vec)
  in lower(#dom\2,dom) { first half }

{
  some functions related to Frobenius-Schur indicator, real/quaternionic indicator,
  and sign of Hermitian form on finite dimensional representation are
  MOVED TO finite_dimensional_signature.at
}

set ht(RealForm G, vec lambda) = int:
rat_as_int(dominant(G,lambda)*G.rho_check*2)

{.tensor product of algebraic reps of complex group of extremal wts gamma1
  and gamma2, returned as KTypePol of corresponding GC K-types }
set tensor_product(RootDatum rd, vec gamma1, vec gamma2) = KTypePol:
   let GC = rd.complexification
   then repn = finite_dimensional(GC,gamma1##gamma2)
   then gamma_rho = ratvec_as_vec(repn.infinitesimal_character - rho(GC))
   then module = minus_1^length(x_open(GC))*
     (GC.null_K_module +
      for x in KGB(GC) do (minus_1^length(x),K_type(x,gamma_rho)) od)
in branch(module, ht(GC,gamma1##gamma2))

{.tensor product of algebraic reps of complex group of extremal wts gamma1
  and gamma2, returned as list of (multiplicity,highest weight)}
set tensor_product_weights(RootDatum rd, vec gamma1, vec gamma2) = [(int,vec)]:
   let r=rd.rank, rho2=two_rho(rd)
in for c@p in tensor_product(rd,gamma1,gamma2)
   do let lambda_rho = p.lambda_minus_rho
   in (split_as_int(c), lower(r,lambda_rho)+upper(r,lambda_rho)+rho2 )
   od

{Finite dimensional algebraic representation with highest weight v as K-type
  of the complexification of G}
set complex_KT (RealForm G, vec v)=KType:
   let GC=complexification(G), n=rank(G)
   in K_type(KGB(GC,0),(v-two_rho(G))##null(n))

{ in the next function |P| is for a pure torus sharing its $X^*$ with |rd| }
{ one can get that torus as |compact_torus(rd.rank)| }
set Demazure (RootDatum rd, int i, KTypePol P) = KTypePol:
(  let alpha=root(rd,i), alpha_v=coroot(rd,i), result = null_K_module(P)
in P +:= { calls +@(KTypePol,[(Split,KType)]) }
     for c@t in P
     do (-c,let (x,lambda)=%t in K_type(x,reflect(rd,i,lambda)-alpha)) od
;  while !=P
   do let (x,lambda) = %first_K_type(P), acc=null_K_module(P)
   then level = alpha_v*lambda
   in if level.< then lambda+:=alpha*(minus_1-level) fi
   ;  assert(!=P[K_type(x,lambda)], @:to_string("absent term for ",(x,lambda)))
   ;  while !=P[K_type(x,lambda+alpha)] do lambda+:=alpha od { back up }
   ;  level := alpha_v*lambda { recompute |level| }
   ;  let top = K_type(x,lambda), bot=K_type(x,lambda+alpha*(minus_1-level))
      then c=P[top]
   in while assert(=P[top]+P[bot],"failing anti-symmetry"); P[top]:=P[bot]:=0
      ; acc[top]+:=c
      ; if =level
        then dont { i.e., quit the loop }
        else acc[bot:=K_type(x,bot.lambda_rho+alpha)]+:=c { mirror contribute }
        ; level>=2 { condition to continue }
        do level-:=2; c+:=P[top:=K_type(x,lambda-:=alpha)]
	fi
      od
   ;  result +:= acc
   od
; result
)

{ The weights of a finite dimensional algebraic representation with extremal
  weight hwt }
set wts_of_fd (RootDatum rd, vec hwt) = KTypePol:
   let Q = KTypePol: K_type(KGB(compact_torus(rank(rd)),0),hwt)
in for s in word(w0(rd)) do Q:=Demazure(rd,s,Q) od; Q





