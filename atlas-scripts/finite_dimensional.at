<Weylgroup.at { dominant }

{ finite dimensional representations of G(C)}
{.whether p is finite dimensional.}
set is_finite_dimensional (Param p) = bool:
  let ssr = semisimple_rank(real_form(p))
  in semisimple_rank(integrality_datum(p))=ssr and #tau(p)=ssr

{.for protecting functions intended for finite dimensional parameters only.}
set fd_only (Param p) = Param:
  assert(is_finite_dimensional(p),"representation is infinite dimensional"); p

{.dimension of finite dimensional representation from its highest weight.}
set dimension (RootDatum rd, ratvec lambda) = int:
( let lambda_dom=dominant(rd,lambda) in
  if lambda != lambda_dom
  then prints("Warning: changed ", lambda, ", to ", lambda_dom
             , " to make it dominant")
  ;  lambda := lambda_dom
  fi
; let rho=rho(rd) in { now apply Weyl's dimension formula }
  product(for a in poscoroots(rd) do (lambda+rho)*a/(rho*a) od).rat_as_int
)

{If split torus in the center not necessarily a vec, this could
fail, leave it for now because otherwise weyl_character_formula.at
fails to load}
{.highest weight of finite dimensional in X^* coordinates.}
set highest_weight_finite_dimensional (Param p) = vec:
   (p.fd_only.dominant.infinitesimal_character - p.root_datum.rho).ratvec_as_vec

{allow ratvec if G has split central torus}
set highest_weight_finite_dimensional_ratvec (Param p) = ratvec:
   (p.fd_only.dominant.infinitesimal_character - p.root_datum.rho)

set on_fundamental_weights (ratvec v,RootDatum rd) = vec:
   (v*simple_coroots(rd)).ratvec_as_vec

{.fundamental weight coordinates of finite dimensional parameter.}
set fundamental_weight_coordinates (Param p) = vec:
   on_fundamental_weights(highest_weight_finite_dimensional(p),root_datum(p))

{use highest_weight_finite_dimensional_ratvec to all split central torus}
{.dimension of finite dimensional parameter.}
set dimension(Param p) = int:
   dimension(root_datum(p),highest_weight_finite_dimensional_ratvec(p))


{ this function depends on a complex root datum, with half-swapping involution }
set complex_LKT (Param p) = vec:
  let dom = dominant(root_datum(p), (1+p.x.involution)*p.lambda.ratvec_as_vec)
  in lower(#dom\2,dom) { first half }

{
  some functions related to Frobenius-Schur indicator, real/quaternionic indicator,
  and sign of Hermitian form on finite dimensional representation are
  MOVED TO finite_dimensional_signature.at
}